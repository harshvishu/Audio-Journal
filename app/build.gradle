apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    def versionPropsFile = file('version.properties')
    if (!versionPropsFile.canRead()) {
        throw new GradleException("Could not read version.properties!")
    }
    def Properties versionProps = new Properties()

    versionProps.load(new FileInputStream(versionPropsFile))
    def code = versionProps['BUILD_VERSION_CODE'].toInteger() + 1
    // INCREMENT BUILD VERSION CODE
    def major = versionProps['MAJOR'].toInteger()
    def minor = versionProps['MINOR'].toInteger()
    def patch = versionProps['PATCH'].toInteger() + 1 // INCREMENT PATCH NUMBER

    // Reset patch and increment minor
    if (patch > 999) {
        minor = minor + 1
        patch = 1
    }

    versionProps['MINOR'] = minor.toString()
    versionProps['PATCH'] = patch.toString()
    versionProps['BUILD_VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
        applicationId rootProject.ext.appId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode code
        versionName "${major}.${minor}.${patch}"
        jackOptions {
            enabled false
        }
        vectorDrawables.useSupportLibrary = true
    }
    productFlavors {
        demo {
            versionNameSuffix '.demo'
            applicationIdSuffix '.demo'
            resValue("string", "file_provider_authority", rootProject.ext.appId + applicationIdSuffix)
        }
        pro {
            versionNameSuffix '.pro'
            applicationIdSuffix '.pro'
            resValue("string", "file_provider_authority", rootProject.ext.appId + applicationIdSuffix)
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", "v ${variant.versionName} (${variant.versionCode})"
    }
    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':hvwaveformview')
    compile project(':hvprogressview')
    compile project(':cameraview')
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion";
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion";
    compile "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion";
    compile "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion";
    compile "com.android.support:percent:$rootProject.ext.supportLibraryVersion";
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.realm:android-adapters:1.4.0'
    compile 'com.bugsnag:bugsnag-android:3.4.0'
    compile 'com.android.support:support-v4:25.1.0'
    compile 'commons-io:commons-io:2.5'
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
}
